plugins {
    id 'org.asciidoctor.jvm.convert' version '3.3.2'
    id 'org.asciidoctor.jvm.gems' version '3.3.2'
    id "me.champeau.jmh" version "0.6.8"
}

allprojects {
    apply plugin: 'java'
    apply plugin: 'checkstyle'
    apply plugin: 'org.asciidoctor.jvm.convert'
    apply plugin: 'org.asciidoctor.jvm.gems'
    apply plugin: "me.champeau.jmh"

    java {
        sourceCompatibility = JavaVersion.VERSION_1_8
        targetCompatibility = JavaVersion.VERSION_1_8
    }

    dependencies {
        repositories {
            mavenCentral()
        }
    }

    plugins.withType(JavaPlugin) {
        dependencies {
            testImplementation 'org.junit.jupiter:junit-jupiter-api:5.7.0'
            testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.7.0'
        }
    }

    checkstyle {
        toolVersion = '9.0'
        ignoreFailures = false
        showViolations = true
        maxWarnings = 0
        configFile = new File("${rootDir}/checkstyle/checkstyle.xml")
    }

    checkstyleTest
            .exclude('**/exclude/*.java')
}

project(":treetops-core") {
    archivesBaseName = "treetops-core"

    apply plugin: 'signing'
    apply plugin: 'maven-publish'

    task javadocJar(type: Jar) {
        classifier = 'javadoc'
        from javadoc
    }

    task sourcesJar(type: Jar) {
        classifier = 'sources'
        from sourceSets.main.allSource
    }

    artifacts {
        archives javadocJar, sourcesJar
    }

    publishing {
        publications {
            mavenJava(MavenPublication) {
                artifactId = 'treetops-core'
                from components.java
                artifact sourcesJar
                artifact javadocJar
                versionMapping {
                    usage('java-api') {
                        fromResolutionOf('runtimeClasspath')
                    }
                    usage('java-runtime') {
                        fromResolutionResult()
                    }
                }
                pom {
                    name = 'treetops'
                    packaging = 'jar'
                    description = 'Treetops, a fast LightGBM tree model interference Java library'

                    licenses {
                        license {
                            name = 'MIT Licence'
                            url = 'https://raw.githubusercontent.com/horoc/treetops/master/LICENSE'
                        }
                    }
                    developers {
                        developer {
                            id = 'chenzhou'
                            name = 'Chen Zhou'
                            url = 'https://github.com/horoc'
                        }
                    }
                    scm {
                        connection = 'https://github.com/horoc/treetops.git'
                        developerConnection = 'git@github.com:horoc/treetops.git'
                        url = 'https://github.com/horoc/treetops'
                    }
                }
            }
        }
        repositories {
            maven {
                name = "OSSRH"
                if (project.version.toString().endsWith("-SNAPSHOT")) {
                    url = "https://s01.oss.sonatype.org/content/repositories/snapshots"
                } else {
                    url = "https://s01.oss.sonatype.org/service/local/staging/deploy/maven2/"
                }
                credentials {
                    username = project.ossrhUsername
                    password = project.ossrhPassword
                }
            }
        }
    }

    signing {
        sign publishing.publications.mavenJava
    }
}

test {
    useJUnitPlatform()
    testLogging {
        events "PASSED", "SKIPPED", "FAILED", "STANDARD_OUT", "STANDARD_ERROR"
    }
}